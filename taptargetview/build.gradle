plugins {
    id 'maven-publish'
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

def artifactoryUser = System.getenv('ARTIFACTORY_BEATGRID_CREDS_USR')
def artifactoryPassword = System.getenv('ARTIFACTORY_BEATGRID_CREDS_PSW')

repositories {
    mavenLocal()
    mavenCentral()
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId "$GROUP"
            artifactId "$POM_ARTIFACT_ID"
            version "$VERSION_NAME"
            afterEvaluate {
                artifact bundleReleaseAar
                artifact sourcesJar
            }
        }
    }
    repositories {
        maven {
            credentials {
                username "${artifactoryUser}"
                password "${artifactoryPassword}"
            }
            url "https://artifactory.beatgrid.net/artifactory/libs-${project.version.endsWith('-SNAPSHOT') ? 'snapshot' : 'release' }-local"
        }
    }
}

android {
    compileSdkVersion defCompileSdkVersion
    buildToolsVersion defBuildToolsVersion

    defaultConfig {
        minSdkVersion defMinSdkVersion
    }
}

dependencies {
    api "androidx.annotation:annotation:$defAndroidXVersion"
    api "androidx.appcompat:appcompat:$defAndroidXVersion"
    implementation "androidx.core:core:1.3.2"
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
